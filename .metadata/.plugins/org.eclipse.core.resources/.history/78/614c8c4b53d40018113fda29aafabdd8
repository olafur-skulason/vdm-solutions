class Environment
types

lineType = <piece> | <order> | <batch>;
inline_piece = nat * token * real * real;
inline_order = nat * nat * Global`Algorithm * nat * real * real;
inline_batch = nat * token;

outline = Global`Event;

instance variables

io : IO := new IO();

inline_pieces : seq of inline_piece := [];
inline_orders : seq of inline_order := [];
inline_batches : seq of inline_batch := [];
outlines : seq of outline := [];

tracker : Tracker := new Tracker({new Location(mk_Global`Location_ID(mk_token(1)), 10), new Location(mk_Global`Location_ID(mk_token(2)), 20)});

operations

public Environment: seq of char * seq of char * seq of char ==> Environment
Environment(file_name_piece, file_name_order, file_name_batch) == (
	def mk_(-,p) = io.freadval[seq of inline_piece](file_name_piece) in
		inline_pieces := p;
	def mk_(-,o) = io.freadval[seq of inline_order](file_name_order) in
		inline_orders := o;
	def mk_(-,b) = io.freadval[seq of inline_batch](file_name_batch) in
		inline_batches := b;
);

public run: () ==> ()
run() ==
(
	while not is_finished()
	do (
		World`time.step_time();
		create_signal();
		tracker.step_time();
		-- Tracker step
	); 
);

private create_signal: () ==> ()
create_signal() == (
	dcl done : bool := false;
	while not done do
	(
		done := true;
		if len inline_pieces > 0 then
			def mk_(p_time, p_id, p_weight, p_length) = hd inline_pieces in
				if p_time < World`time.get_time() then (
						tracker.add_piece(mk_Global`Piece(p_id, p_weight, p_length, 0, nil));
						inline_pieces := tl inline_pieces;
						done := false;
				);
		if len inline_orders > 0 then
			def mk_(o_time, o_priority, o_alg, o_count, o_min_weight, o_max_weight) = hd inline_orders in
				if o_time < World`time.get_time() then (
					tracker.add_order(mk_Global`Order(o_priority, o_alg, o_count, o_min_weight, o_max_weight));
					inline_orders := tl inline_orders;
					done := false;
				);
		if len inline_batches > 0 then
			def mk_(b_time, b_id) = hd inline_batches in
				if b_time < World`time.get_time() then (
						tracker.sign_batch(mk_Global`Location_ID(b_id));
						inline_batches := tl inline_batches;
						done := false;
				)
	);
);

private pure is_finished: () ==> bool
is_finished() == return len inline_pieces = 0 and len inline_orders = 0 and len inline_batches = 0;

functions
traces
end Environment 

